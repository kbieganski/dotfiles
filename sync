#!/bin/env zsh

dotfiles_dir=$(dirname ${(%):-%N})
pkg_dir=pkg
pacman_req_pkgs=$pkg_dir/pacman.txt
pacman_cur_pkgs=$pkg_dir/.pacman.txt
secret_dir=ext/dotfiles-secret

function section {
    echo "=> \e[34m$*\e[0m"
}

function silent {
    local temp=$(mktemp)
    $* &> $temp
    if [ $? -ne 0 ]; then
        < $temp
        echo "\e[31mFailed: '\e[0m$*\e[31m'\e[0m"
        exit 1
    fi
}

cd $dotfiles_dir

section "Pulling changes"
old_commit=$(git rev-parse HEAD)
silent git pull
silent git submodule update --init --recursive
silent stow $dotfiles_dir
new_commit=$(git rev-parse HEAD)

section "Deleting system packages"
touch $pacman_cur_pkgs
pacman_del_pkgs=$(comm -23 <(sort < $pacman_cur_pkgs) <(sort < $pacman_req_pkgs))
if [ -n "$pacman_del_pkgs" ]; then
    silent sudo pacman -Rs --noconfirm ${=pacman_del_pkgs}
fi

section "Installing system packages"
pacman_new_pkgs=$(comm -13 <(sort < $pacman_cur_pkgs) <(sort < $pacman_req_pkgs))
if [ -n "$pacman_new_pkgs" ]; then
    silent sudo pacman -Sy --noconfirm ${=pacman_new_pkgs}
fi

section "Saving system package list"
pacman -Qqne | rg -v "(amd|intel|nvidia|cuda|wacom)" | sort > $pacman_cur_pkgs
cp $pacman_req_pkgs $pacman_cur_pkgs
git add $pacman_req_pkgs

section "Syncing Neovim plugins"
silent nvim '+Lazy! sync' +qa

section "Updating bat cache"
silent bat cache --build

section "Reloading i3"
silent i3-msg reload

section "Installing Rust packages"
for toolchain in stable nightly; do
    silent rustup toolchain install --profile default $toolchain
    while read pkg; do
        silent rustup +$toolchain component add $pkg
    done < $pkg_dir/rustup-$toolchain.txt
    while read pkg; do
        silent cargo +$toolchain install $pkg
    done < $pkg_dir/cargo.txt
done

section "Installing Go packages"
while read pkg; do
    silent go install $pkg
done < $pkg_dir/go.txt

if [ -n "$(git -C $secret_dir diff --staged --name-only)" ]; then
    section "Committing and pushing changes"
    silent git -C $secret_dir save
    silent git -C $secret_dir push
    silent git add $secret_dir
    silent git save
    silent git push
elif [ -n "$(git diff --staged --name-only)" ]; then
    section "Committing and pushing changes"
    silent git save
    silent git push
fi

echo "\e[32mDone.\e[0m"
