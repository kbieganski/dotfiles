#!/bin/env zsh

dotfiles_dir=$(dirname ${(%):-%N})
pkg_dir=pkg
pacman_pkg_list=$pkg_dir/pacman.txt
secret_dir=ext/dotfiles-secret

function section {
    echo "=> \e[34m$*\e[0m"
}

function silent {
    local temp=$(mktemp)
    $* &> $temp
    if [ $? -ne 0 ]; then
        < $temp
        echo "\e[31mFailed: '\e[0m$*\e[31m'\e[0m"
        exit 1
    fi
}

cd $dotfiles_dir

section "Pulling changes"
old_commit=$(git rev-parse HEAD)
silent git pull
silent git submodule update --init --recursive
silent stow $dotfiles_dir
new_commit=$(git rev-parse HEAD)

section "Deleting system packages"
pacman_del_pkgs=$(comm -23 <(git show $old_commit:$pacman_pkg_list | sort) <(sort < $pacman_pkg_list))
if [ -n "$pacman_del_pkgs" ]; then
    silent sudo pacman -Rs --noconfirm ${=pacman_del_pkgs}
fi

section "Installing system packages"
pacman_new_pkgs=$(comm -13 <(pacman -Qqne | sort) <(sort < $pacman_pkg_list))
if [ -n "$pacman_new_pkgs" ]; then
    silent sudo pacman -Sy --noconfirm ${=pacman_new_pkgs}
fi

pacman -Qqne | rg -v "(amd|intel|nvidia|cuda|wacom)" | sort > $pacman_pkg_list

section "Syncing Neovim plugins"
silent nvim '+Lazy! sync' +qa

section "Updating bat cache"
silent bat cache --build

section "Installing Rust packages"
for toolchain in stable nightly; do
    silent rustup toolchain install --profile default $toolchain
    while read pkg; do
        silent rustup +$toolchain component add $pkg
    done < $pkg_dir/rustup-$toolchain.txt
    while read pkg; do
        silent cargo +$toolchain install $pkg
    done < $pkg_dir/cargo.txt
    while read pkg; do
        silent cargo +$toolchain install --git $pkg
    done < $pkg_dir/cargo-git.txt
done

if [ -n "$(git -C $secret_dir diff --staged --name-only)" ]; then
    section "Committing and pushing changes"
    silent git -C $secret_dir save
    silent git -C $secret_dir push
    silent git add $secret_dir
    silent git save
    silent git push
elif [ -n "$(git diff --staged --name-only)" ]; then
    section "Committing and pushing changes"
    silent git save
    silent git push
fi

echo "\e[32mDone.\e[0m"
